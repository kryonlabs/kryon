// Row Alignments Demo - Using Object Literals
// Demonstrates different mainAxisAlignment values for Row layouts

const alignments = [
  { name: "start (flex-start)", value: "start", gap: 20,
    colors: ["#ef4444", "#dc2626", "#b91c1c"] },
  { name: "center", value: "center", gap: 20,
    colors: ["#06b6d4", "#0891b2", "#0e7490"] },
  { name: "end (flex-end)", value: "end", gap: 20,
    colors: ["#8b5cf6", "#7c3aed", "#6d28d9"] },
  { name: "spaceEvenly", value: "spaceEvenly", gap: 0,
    colors: ["#64748b", "#475569", "#334155"] },
  { name: "spaceAround", value: "spaceAround", gap: 0,
    colors: ["#f59e0b", "#d97706", "#b45309"] },
  { name: "spaceBetween", value: "spaceBetween", gap: 0,
    colors: ["#10b981", "#059669", "#047857"] }
]

App {
  width = 800
  height = 740
  title = "Row Alignments Demo"
  background = "#f8fafc"

  Column {
    justifyContent = "start"
    alignItems = "stretch"
    gap = 20
    padding = 20
    width = 100%
    height = 100%

    static {
      for item in alignments {
        Container {
          width = 760
          height = 100
          background = "#ffffff"
          padding = 10

          Column {
            justifyContent = "start"
            alignItems = "start"
            gap = 8

            Text {
              text = item.name
              fontSize = 12
              fontWeight = "bold"
            }

            Container {
              width = 740
              height = 60
              background = "#f1f5f9"
              padding = 8

              Row {
                justifyContent = item.value
                alignItems = "center"
                gap = item.gap

                Container {
                  width = 80
                  height = 40
                  background = item.colors[0]
                }

                Container {
                  width = 100
                  height = 40
                  background = item.colors[1]
                }

                Container {
                  width = 90
                  height = 40
                  background = item.colors[2]
                }
              }
            }
          }
        }
      }
    }
  }
}
